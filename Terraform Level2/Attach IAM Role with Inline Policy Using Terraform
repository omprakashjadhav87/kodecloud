The Nautilus DevOps team is setting up IAM-based access control for internal AWS resources. They need to create an IAM Role and an IAM Policy using Terraform and attach 
the policy to the role.

Create an IAM Role named nautilus-role.

Create an IAM Policy named nautilus-policy that allows listing EC2 instances.

Attach the policy to the role

Create the main.tf file (do not create a separate .tf file) to provision a Role, policy and attach it.

Use the variables.tf file with the following:

KKE_ROLE_NAME: name of the role.
KKE_POLICY_NAME: name of the policy.
Use terraform.tfvarsfile to input the role and policy names.

Use outputs.tf file to output the following:

kke_iam_role_name: name of the role created.
kke_iam_policy_name: name of the policy ceated.

Notes:

The Terraform working directory is /home/bob/terraform.

Right-click under the EXPLORER section in VS Code and select Open in Integrated Terminal to launch the terminal.

Before submitting the task, ensure that terraform plan returns No changes. Your infrastructure matches the configuration.

Solutions:-

main.tf

resource "aws_iam_role" "kke_role" {
  name = var.KKE_ROLE_NAME

  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Action = "sts:AssumeRole",
        Effect = "Allow",
        Principal = {
          Service = "ec2.amazonaws.com"
        }
      }
    ]
  })
}

resource "aws_iam_policy" "kke_policy" {
  name        = var.KKE_POLICY_NAME
  description = "Policy to allow listing EC2 instances"

  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Action = [
          "ec2:DescribeInstances"
        ],
        Effect   = "Allow",
        Resource = "*"
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "kke_role_policy_attachment" {
  role       = aws_iam_role.kke_role.name
  policy_arn = aws_iam_policy.kke_policy.arn
}

=========================================================================

variables.tf

variable "KKE_ROLE_NAME" {
  description = "Name of the IAM Role"
  type        = string
}

variable "KKE_POLICY_NAME" {
  description = "Name of the IAM Policy"
  type        = string
}

=======================================================================

terraform.tfvars

KKE_ROLE_NAME  = "nautilus-role"
KKE_POLICY_NAME = "nautilus-policy"

=======================================================================

outputs.tf

output "kke_iam_role_name" {
  value = aws_iam_role.kke_role.name
}

output "kke_iam_policy_name" {
  value = aws_iam_policy.kke_policy.name
}

====================================================================

Then run:
terraform init
terraform apply
terraform plan
