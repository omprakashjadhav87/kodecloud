The Nautilus DevOps team is implementing a messaging system in AWS. They want to create an SNS topic and an SQS queue. The team needs to subscribe the SQS queue to the SNS topic so that any messages
sent to the SNS topic will be delivered to the SQS queue.

Create an SNS topic named xfusion-sns-topic.

Create an SQS queue named xfusion-sqs-queue.

Subscribe the SQS queue to the SNS topic.

Use the main.tf file (do not create a separate .tf file) to provision the SNS topic and SQS queue.

Create the outputs.tf file, and use the following:

The ARN of the SNS topic using the output variable kke_sns_topic_arn.

The URL of the SQS queue using the output variable kke_sqs_queue_url.


Notes:

The Terraform working directory is /home/bob/terraform.

Right-click under the EXPLORER section in VS Code and select Open in Integrated Terminal to launch the terminal.

Before submitting the task, ensure that terraform plan returns No changes. Your infrastructure matches the configuration.

Solutions:- 

✅ main.tf

provider "aws" {
  region = "us-east-1"
}

resource "aws_sns_topic" "xfusion_sns_topic" {
  name = "xfusion-sns-topic"
}

resource "aws_sqs_queue" "xfusion_sqs_queue" {
  name = "xfusion-sqs-queue"
}

resource "aws_sqs_queue_policy" "xfusion_sqs_policy" {
  queue_url = aws_sqs_queue.xfusion_sqs_queue.id

  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Effect = "Allow",
        Principal = "*",
        Action = "SQS:SendMessage",
        Resource = aws_sqs_queue.xfusion_sqs_queue.arn,
        Condition = {
          ArnEquals = {
            "aws:SourceArn" = aws_sns_topic.xfusion_sns_topic.arn
          }
        }
      }
    ]
  })
}

resource "aws_sns_topic_subscription" "xfusion_sns_subscription" {
  topic_arn = aws_sns_topic.xfusion_sns_topic.arn
  protocol  = "sqs"
  endpoint  = aws_sqs_queue.xfusion_sqs_queue.arn
}

=========================================================

✅ outputs.tf

output "kke_sns_topic_arn" {
  value = aws_sns_topic.xfusion_sns_topic.arn
}

output "kke_sqs_queue_url" {
  value = aws_sqs_queue.xfusion_sqs_queue.id
}


============================================================


cd /home/bob/terraform
terraform init
terraform apply
terraform plan
